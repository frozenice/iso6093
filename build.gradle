buildscript {
  ext.dokka_version = '0.9.17' // https://bintray.com/kotlin/dokka/dokka

  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
  }
}

plugins {
  // https://bintray.com/jfrog/jfrog-jars/gradle-bintray-plugin
  id "com.jfrog.bintray" version "1.8.4"

  id 'kotlin-multiplatform' version '1.3.0'
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'

group 'de.frozenice'
version '0.1.0'
description 'A kotlin library to parse and format floating-point numbers according to ISO 6093.'

repositories {
  mavenCentral()
}

kotlin {
  targets {
    fromPreset(presets.jvm, "jvm") {
      compilations.all {
        tasks[compileKotlinTaskName].kotlinOptions {
          jvmTarget = "1.8"
        }
      }

      mavenPublication {
        //artifact sourcesJar
        artifact javadocJar
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
      }
    }
    commonTest {
      dependencies {
      }
    }

    jvmMain {
      dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
      }
    }
    jvmTest {
      dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-reflect'

        // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        // https://mvnrepository.com/artifact/io.kotlintest/kotlintest-runner-junit5
        implementation group: 'io.kotlintest', name: 'kotlintest-runner-junit5', version: '3.1.10'
      }
    }
  }
}

jvmTest {
  useJUnitPlatform()
}

dokka {
  kotlinTasks {
    ["compileKotlinJvm"]
  }
  outputFormat = 'javadoc'

  jdkVersion = 8
  linkMapping {
    dir = "src/main/kotlin"
    url = "https://github.com/frozenice/iso6093/tree/master/src/main/kotlin"
    suffix = "#L"
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from dokka
}


//publishing {
//  publications {
//    lib(MavenPublication) {
//      from components.java
//      artifact sourcesJar
//      artifact javadocJar
//    }
//  }
//}

bintray {
  user = property("bintray.user")
  key = property("bintray.apikey")

  publish = true
  override = false

  publications = ["KotlinMultiplatformPublication"]

  pkg {
    repo = "kotlin"
    name = project.name

    version {
      name = project.version

      gpg {
        sign = true
      }

      mavenCentralSync {
        sync = true
      }
    }
  }
}
