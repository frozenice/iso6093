group 'de.frozenice'
version '0.0.1'
description 'A kotlin library to parse and format floating-point numbers according to ISO 6093.'

buildscript {
  ext.kotlin_version = '1.2.0'
  ext.dokka_version = '0.9.15'

  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
  }
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'org.jetbrains.dokka'

repositories {
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

  testCompile "io.kotlintest:kotlintest:2.0.7" // includes only kotlin-reflect:1.1.51
  testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from dokka
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name project.name
        description project.description
        packaging 'jar'
        url 'https://github.com/frozenice/iso6093'

        scm {
          connection 'scm:git:https://github.com/frozenice/iso6093.git'
          developerConnection 'scm:git:https://github.com/frozenice/iso6093.git'
          url 'https://github.com/frozenice/iso6093'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            name 'David Kirstein'
            email 'frozenice@frozenice.de'
          }
        }
      }
    }
  }
}

nexusStaging {
  username = ossrhUsername
  password = ossrhPassword
}

dokka {
  outputFormat = 'javadoc'

  jdkVersion = 8
  linkMapping {
    dir = "src/main/kotlin"
    url = "https://github.com/frozenice/iso6093/tree/master/src/main/kotlin"
    suffix = "#L"
  }
}
